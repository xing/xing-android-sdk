/*
 * Copyright (ะก) 2016 XING SE (http://xing.com/)
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
apply plugin: 'com.android.application'

Properties local_properties = new Properties()
File localPropertiesFile = project.rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    local_properties.load(localPropertiesFile.newDataInputStream())
}

android {
    compileSdkVersion versions.androidCompileSdk
    buildToolsVersion versions.androidBuildTools

    defaultConfig {
        applicationId GROUP_ID + ".sample"

        minSdkVersion versions.androidMinSdk
        targetSdkVersion versions.androidTargetSdk

        versionCode = VERSION_CODE
        versionName = VERSION

        /**
         * === KEYS ===
         * Define in your local.properties file the next two keys:
         *      - oauth_consumer_key
         *      - oauth_consumer_secret
         *
         * to obtain them visit https://dev.xing.com/
         */
        buildConfigField "String", "OAUTH_CONSUMER_KEY", "\"" + (local_properties.oauth_consumer_key ?: "") + "\""
        buildConfigField "String", "OAUTH_CONSUMER_SECRET", "\"" + (local_properties.oauth_consumer_secret ?: "") + "\""
    }

    buildTypes {
        release {
            minifyEnabled false
        }
    }

    lintOptions {
        abortOnError true
        disable 'InvalidPackage'
    }

    packagingOptions {
        exclude 'LICENSE.txt'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/rxjava.properties'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

dependencies {
    compile project(':api-client-android-oauth')

    compile libraries.okhttpLoggingInterceptor
    compile libraries.supportV4
    compile libraries.supportAppCompat
    compile libraries.supportDesign
    compile libraries.supportCardView
    compile libraries.supportRecyclerView
    compile libraries.glide
}
